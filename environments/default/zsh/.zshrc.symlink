HYPHEN_INSENSITIVE="true"
DISABLE_AUTO_UPDATE="true"
COMPLETION_WAITING_DOTS="true"
ZSH_THEME=powerlevel9k/powerlevel9k

# Which plugins would you like to load? (plugins can be found in ~/.oh-my-zsh/plugins/*)
# Custom plugins may be added to ~/.oh-my-zsh/custom/plugins/
# Example format: plugins=(rails git textmate ruby lighthouse)
# Add wisely, as too many plugins slow down shell startup.
plugins=(git colored-man colorize brew osx zsh-syntax-highlighting jira mvn fasd)

if [[ -d /usr/local/bin ]] ; then
  export PATH="$PATH:/usr/local/bin"
fi
if [[ -d /usr/sbin ]] ; then
  export PATH="$PATH:/usr/sbin"
fi
if [[ -d /sbin ]] ; then
  export PATH="$PATH:/sbin"
fi

if [[ -z "$ZSH" ]] ; then
    export ZSH=$HOME/.oh-my-zsh
fi

if [[ -e "$ZSH/oh-my-zsh.sh" ]] ; then
    source $ZSH/oh-my-zsh.sh
else
    echo "WARNING: oh my zsh not found at: '$ZSH/oh-my-zsh.sh'"
fi

if [[ -z "$DOTFILES_HOME" ]] ; then
    if which realpath >/dev/null; then
        ZSHRC_LOCATION=`realpath ~/.zshrc`
    fi
    if [[ -e $ZSHRC_LOCATION ]] ; then
        ZSH_HOME=`dirname $ZSHRC_LOCATION`
        CHECK_DOTFILES_HOME=`dirname $ZSH_HOME`
        if [[ -e $CHECK_DOTFILES_HOME/bootstrap.py ]] ; then
            export DOTFILES_HOME=$CHECK_DOTFILES_HOME
        fi
    fi
fi
if [[ -z "$DOTFILES_HOME" ]] ; then
    export DOTFILES_HOME="$HOME/.dotfiles"
fi
if [[ -d $DOTFILES_HOME ]] ; then
    if [[ -e "$HOME/.dotfiles-environments" ]] ; then
        for ENVIRONMENT_NAME in $(cat $HOME/.dotfiles-environments); do
            for ZSH_FILE in $DOTFILES_HOME/environments/$ENVIRONMENT_NAME/**/*.zsh(N); do
                source $ZSH_FILE
            done
            for BIN_DIRECTORY in $DOTFILES_HOME/environments/$ENVIRONMENT_NAME/**/bin(/N); do
                export PATH="$PATH:$BIN_DIRECTORY"
            done
        done
    else
        for ZSH_FILE in $DOTFILES_HOME/environments/default/**/*.zsh(N); do
            source $ZSH_FILE
        done
    fi
else
    echo "WARNING: No dotfiles directory found at: '$DOTFILES_HOME'"
fi

# Overwrite all settings with local settings that may be stored in the users
# home directory which are *not* under version control inside the dotfiles
if [[ -d ~/.zshrc.local ]] ; then
    for ZSH_FILE in ~/.zshrc.local/**/*.zsh(N); do
        source $ZSH_FILE
    done
    for BIN_DIRECTORY in ~/.zshrc.local/**/bin(/N); do
        export PATH="$PATH:$BIN_DIRECTORY"
    done
fi
